#version 460 core

#extension GL_EXT_samplerless_texture_functions : require

layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;

layout(set = 0, binding = 0) uniform texture2D in_curr_color; // upper level (blurred result of the downsampling)
layout(set = 0, binding = 1) uniform sampler2D in_prev_color; // lower level (summed result of previous up iteration, has 1/2 of the size)
layout(set = 0, binding = 2, r11f_g11f_b10f) uniform writeonly restrict image2D out_color; // upper level

layout(push_constant) uniform PushConstants {
    float prevFactor;
    float currFactor;
    int lastPass;
} cParams;

// 9-tap bilinear upsampler (tent filter)
// . . . . . . .
// . A . B . C .
// . . . . . . .
// . D . E . F .
// . . . . . . .
// . G . H . I .
// . . . . . . .
vec4 sampleTent(vec2 tex_coord, vec2 texel_size) {
    vec4 a = textureLod(in_prev_color, vec2(tex_coord + ivec2(-1, -1)) * texel_size, 0.0);
    vec4 b = textureLod(in_prev_color, vec2(tex_coord + ivec2( 0, -1)) * texel_size, 0.0);
    vec4 c = textureLod(in_prev_color, vec2(tex_coord + ivec2( 1, -1)) * texel_size, 0.0);
    vec4 d = textureLod(in_prev_color, vec2(tex_coord + ivec2(-1,  0)) * texel_size, 0.0);
    vec4 e = textureLod(in_prev_color, vec2(tex_coord + ivec2( 0,  0)) * texel_size, 0.0);
    vec4 f = textureLod(in_prev_color, vec2(tex_coord + ivec2( 1,  0)) * texel_size, 0.0);
    vec4 g = textureLod(in_prev_color, vec2(tex_coord + ivec2(-1,  1)) * texel_size, 0.0);
    vec4 h = textureLod(in_prev_color, vec2(tex_coord + ivec2( 0,  1)) * texel_size, 0.0);
    vec4 i = textureLod(in_prev_color, vec2(tex_coord + ivec2(-1,  1)) * texel_size, 0.0);

    vec4 result = 1*a + 2*b + 1*c;
    result +=     2*d + 4*e + 2*f;
    result +=     1*g + 2*h + 1*i;

    return result * (1.0 / 16.0);
}

void main() {
    ivec2 tex_coord = ivec2(gl_GlobalInvocationID.xy );

    vec4 bloom = sampleTent(vec2((tex_coord + ivec2(1)) / 2), vec2(1.0) / textureSize(in_prev_color, 0));
    vec4 color = cParams.prevFactor * bloom;
    if (cParams.lastPass != 1) {
        color += cParams.currFactor * texelFetch(in_curr_color, tex_coord, 0);
    }
    imageStore(out_color, tex_coord, color);
}